# Modifies the item in storage

data modify storage tcc:temp root.ingredient set from block ~ ~ ~ Items[{Slot:3b}]
execute if data storage tcc:temp root.ingredient.components."minecraft:custom_data".tcc{id:"sightless_eye"} if data storage tcc:temp root.item{id:"minecraft:potion",components:{"minecraft:potion_contents":{"potion":"minecraft:water"}}} run data modify storage tcc:temp root.item.components."minecraft:potion_contents".potion set value "minecraft:mundane"
execute if data storage tcc:temp root.ingredient.components."minecraft:custom_data".tcc{id:"amethyst_dust"} unless data storage tcc:temp root.item{id:"minecraft:potion"} if data storage tcc:temp root.item.components."minecraft:potion_contents"{"potion":"minecraft:awkward"} run data modify storage tcc:temp root.item.components."minecraft:potion_contents".potion set value "minecraft:mundane"
execute if data storage tcc:temp root.ingredient.components."minecraft:custom_data".tcc{id:"amethyst_dust"} if data storage tcc:temp root.item{id:"minecraft:potion"} run function tcc:block/brewing_stand/brewing/dilute_potion/check_diluted_type
execute if data storage tcc:temp root.ingredient.components."minecraft:custom_data".tcc{id:"sightless_eye"} if data storage tcc:temp root.item.components."minecraft:potion_contents"{"potion":"minecraft:awkward"} run function tcc:block/brewing_stand/brewing/brew_custom_potion/blind_rage
execute if data storage tcc:temp root.ingredient.components."minecraft:custom_data".tcc{id:"warped_wart"} if data storage tcc:temp root.item.components."minecraft:potion_contents"{"potion":"minecraft:water"} run function tcc:block/brewing_stand/brewing/brew_custom_potion/nausea
execute if data storage tcc:temp root.ingredient.components."minecraft:custom_data".tcc{id:"warped_wart"} if data storage tcc:temp root.item.components."minecraft:potion_contents"{"potion":"minecraft:awkward"} run function tcc:block/brewing_stand/brewing/brew_custom_potion/nausea
execute if data storage tcc:temp root.ingredient.components."minecraft:custom_data".tcc{id:"heartbeet"} if data storage tcc:temp root.item.components."minecraft:potion_contents"{"potion":"minecraft:awkward"} run function tcc:block/brewing_stand/brewing/brew_custom_potion/fortitude
execute if data storage tcc:temp root.ingredient.components."minecraft:custom_data".tcc{id:"enchanted_golden_carrot"} if data storage tcc:temp root.item.components."minecraft:potion_contents"{"potion":"minecraft:awkward"} run data modify storage tcc:temp root.item.components."minecraft:potion_contents".potion set value "minecraft:long_night_vision"
execute if data storage tcc:temp root.ingredient{id:"minecraft:redstone"} if data storage tcc:temp root.item.components."minecraft:custom_data".tcc.potion{can_extend:1b} run function tcc:block/brewing_stand/brewing/edit_potion/extend
execute if data storage tcc:temp root.ingredient{id:"minecraft:glowstone_dust"} if data storage tcc:temp root.item.components."minecraft:custom_data".tcc.potion{can_amplify:1b} run function tcc:block/brewing_stand/brewing/edit_potion/amplify
execute if data storage tcc:temp root.ingredient{id:"minecraft:gunpowder"} if data storage tcc:temp root.item.components."minecraft:custom_data".tcc.potion.id if data storage tcc:temp root.item{id:"minecraft:potion"} run function tcc:block/brewing_stand/brewing/edit_potion/make_splash
execute if data storage tcc:temp root.ingredient{id:"minecraft:dragon_breath"} if data storage tcc:temp root.item.components."minecraft:custom_data".tcc.potion.id if data storage tcc:temp root.item{id:"minecraft:splash_potion"} run function tcc:block/brewing_stand/brewing/edit_potion/make_lingering